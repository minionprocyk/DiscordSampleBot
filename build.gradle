plugins {
    id 'idea'
    id 'java'
    id 'antlr'
    id 'application'
}
generateGrammarSource {
    maxHeapSize = '1g'
    outputs.cacheIf { true }

    final PARSER_PACKAGE_NAME = 'com.procyk.industries'
    arguments += ["-listener", "-package", PARSER_PACKAGE_NAME]

    doLast {
        def parserFilePattern = 'DiscoLang*'
        def outputPath = generateGrammarSource.outputDirectory.canonicalPath
        def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
        file(parserPackagePath).mkdirs()
        copy {
            from outputPath
            into parserPackagePath
            include parserFilePattern
        }
        delete fileTree(outputPath) {
            include parserFilePattern
        }
    }
}
idea {
    module {
        downloadJavadoc=true
        downloadSources=true
    }
}
repositories {
   mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}


test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter'
        failFast = true
    }
}


dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    implementation 'org.clojure:clojure:1.10.1'
    implementation group: 'com.google.firebase', name: 'firebase-admin', version: '6.7.0'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20190128-1.28.0'
    implementation 'net.dv8tion:JDA:4.1.1_136'
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.2'
    implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.2.2'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    implementation 'com.sedmelluq:lavaplayer:1.3.47'

    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.7.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.0.0'

}
mainClassName="com.procyk.industries.main.Launcher"
