plugins {
    id 'idea'
    id 'java'
    id 'antlr'
    id 'application'
    id "com.github.ben-manes.versions" version "0.33.0"
}
generateGrammarSource {
    maxHeapSize = '1g'
    outputs.cacheIf { true }

    final PARSER_PACKAGE_NAME = 'com.procyk.industries'
    arguments += ["-listener", "-package", PARSER_PACKAGE_NAME]

    doLast {
        def parserFilePattern = 'DiscoLang*'
        def outputPath = generateGrammarSource.outputDirectory.canonicalPath
        def parserPackagePath = "${outputPath}/${PARSER_PACKAGE_NAME.replace('.', '/')}"
        file(parserPackagePath).mkdirs()
        copy {
            from outputPath
            into parserPackagePath
            include parserFilePattern
        }
        delete fileTree(outputPath) {
            include parserFilePattern
        }
    }
}
idea {
    module {
        downloadJavadoc=true
        downloadSources=true
    }
}
repositories {
   mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}


test {
    useJUnitPlatform {
        excludeTags 'slow'
        includeEngines 'junit-jupiter'
        failFast = true
    }
}

dependencies {
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3.2'
    antlr "org.antlr:antlr4:4.7.2"
    implementation group: 'org.clojure', name: 'clojure', version: '1.10.1'
    implementation group: 'com.google.firebase', name: 'firebase-admin', version: '7.0.0'
    implementation group: 'com.google.apis', name: 'google-api-services-youtube', version :'v3-rev20200618-1.30.9'
    implementation group: 'net.dv8tion', name: 'JDA', version: '4.2.0_196'
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.3'
    implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.2.3'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.61'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.0.0'

}

mainClassName="com.procyk.industries.main.Launcher"
